include "../../aimlgen.ang"


[
    "" "RCLICK *" cat,
    "Ah, a very nice and deadly item." `RCLICKWEAPON cat,
    
    `RCLICKWEAPON sr "RCLICK * SWORD" cat,
    `RCLICKWEAPON sr "RCLICK * AXE" cat,
    
    [ "RCLICK", `itemheld g] sr "RIGHTCLICK" cat,
    
    [
        # A pome. Set up some words we can reuse,
        # then do the pome.
        `REGEN sr,
        `POEMROOT sr
    ]
    `POEM cat,
    
    # regenerate the "remembered" thingies
    [
        `RANDNOUN sr `s1 s,
        `RANDNOUN sr `s2 s,
        `RANDNOUN sr `s3 s,
        `RANDADJ sr `a1 s,
        `RANDADJ sr `a2 s
    ] think `REGEN cat,
    
 
 
    # Made up of lines all using the words we thought up.
    # Recurses and then stops after a bit. Hopefully.

    [
        `LINE sr,
        [`LINE, `POEMROOT] random sr
    ] `POEMROOT cleancat,
    
    # clause generator. This generates a "sentence" which could
    # be an actual sentence or could have a "however, but..." put
    # on the end
    
    [
        [
            [`s1 g,`s2 g,`s3 g, `RANDNOUN sr] random, "is",
            [`a1 g,`a2 g, `RANDADJ sr] random
        ]
    ] 
    random `CLAUSE cat,
    
    # and here we have the heart of the matter - the line
    # generator. Generate a random line type and put "." on the end.
    [
        `RANDBEGIN sr,
        [
            [`CLAUSE sr clean, "."],
            [`CLAUSE sr clean, `REGEN sr, "but", `CLAUSE sr, "."]
        ] random
    ]  clean `LINE cat,
    
    # A random beginning.
    ["Behold!","I see.","See!","Hear me!", "Know this.", "Thus it is.",
        "","","","",""] random
    `RANDBEGIN cat,
    
    # people, singular
    ["a man","a woman","a child","the monster","the villager","the soldier","mankind","all the world",
        "the poet","every man"] random `RANDPERSON cat,
    
    # objects, real
    ["the moon","the sun","the earth","the world","the Castle","a dream","the stone","the rock",
        "the Underworld","each block"] random `RANDOBJECT cat,
    
    # objects, abstract
    ["poetry","truth","my wisdom","war","peace","music"] random `RANDABSOBJ cat,
    
    # Some noun phrases
    [`RANDPERSON,`RANDOBJECT,`RANDABSOBJ ] ?sr map random `RANDNOUN cat,
    
    # adjectives.
    ["dark","light","heavy","grey","bright","unknowable","true","false",
        "alive","wise","dead","knowable","unknowable","constant",
        "unwise","clear","right","wrong","everchanging","musical",
        `RANDCOL sr,
        [ `RANDPREP sr, `RANDNOUN sr]
    ] random `RANDADJ cat,
 
    # colours
    ["red","green","black","white"] random
    `RANDCOL cat,
 
    # prepositions
    ["within","outside","of","under","over","one with"] random 
    `RANDPREP cat,

 
    `POEM sr `RANDSAY cat
] 
aiml done

